buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "io.qameta.allure:allure-gradle:2.8.1"
    }
}

plugins {
    id 'java'    
    id 'application'
}

apply plugin: 'io.qameta.allure'

allure {
    version = '2.13.0'
    autoconfigure = true
    aspectjweaver = true
    allureJavaVersion = '2.13.0'
}

test {
    testLogging.showStandardStreams = true
    
    useTestNG {
        suites 'src/test/resources/testng-suite.xml'
    }

    options {
        useDefaultListeners = false
        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    }

    loadProperties()
}

dependencies {    
    compile 'com.google.guava:guava:23.0'
    compile 'org.seleniumhq.selenium:selenium-remote-driver:3.141.59'
    compile 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
    compile 'org.seleniumhq.selenium:selenium-firefox-driver:3.141.59'
    compile 'org.seleniumhq.selenium:selenium-support:3.141.59'
    compile 'org.testcontainers:selenium:1.12.2'
    compile 'com.google.inject:guice:4.0-beta5'
    compile 'io.qameta.allure:allure-testng:2.7.0'
    testCompile 'org.testng:testng:6.10'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile("org.uncommons:reportng:1.1.4") { exclude group: "org.testng", module: "testng" }
}

repositories {
    jcenter()
}

def loadProperties() {
    def props = [:]
    props.put('browser', findProperty('browser') ?: '')
    props.put('orchestrator', findProperty('orchestrator') ?: '')
    props.put('record', findProperty('record') ?: '')
    props.forEach {key, value ->
        if (value != '') {
            test.systemProperty("${key}", "${value}")
        }
    }
}